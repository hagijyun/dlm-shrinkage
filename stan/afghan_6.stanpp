// -*- mode: stan -*-
// Afghanistan: Integrated Local Trend (Horseshoe) + Trig Seasonal
# include "jeffreys.stanh"
# include "kalman_seq.stanh"
data {
  // dimensions
  int n; // number of observations 
  int p; // variables // p = 1
  int m; // states // m = 4
  int r; // disturbances // r = 3
  // system matrices
  cov_matrix[m] P1;
  vector[m] a1;
  // data
  matrix[1, n] y;
  matrix[m, m] T;
  matrix[p, m] Z;
  matrix[m, r] R;
  vector[p] c;
  vector[m] d;
}
parameters {
  real<lower=0.0> sigma2;
  vector<lower=0.0>[2] tau;
  vector<lower=0.0>[n] lambda;
}
transformed parameters {
  matrix[r, r] Q[n];
  vector<lower=0.0>[p] H;
  H[1] <- sigma2;
  for (i in 1:n) {
    real tau2_2;
    vector<lower=0.0>[r] Q_diag;
    tau2_2 <- pow(tau[2], 2);
    Q_diag[1] <- pow(tau[1] * lambda[i], 2);
    Q_diag[2] <- tau2_2;
    Q_diag[3] <- tau2_2;
    Q[i] <- diag_matrix(Q_diag);
  }
}
model {
  matrix[r, r] RQR[n];
  for (i in 1:n) {
    RQR[i] <- R * Q[i] * R';
  }   
  KALMAN_SEQ_Q(y, T, Z, H, RQR, a1, P1, c, d);
  JEFFREYS(sigma2);
  tau ~ cauchy(0, sqrt(sigma2));
  lambda ~ cauchy(0, 1);
}
