// -*- mode: stan -*-
// Afghanistan: Local Trend + Trig Seasonal
# include "jeffreys.stanh"
# include "kalman_seq.stanh"
data {
  // dimensions
  int n; // number of observations 
  int p; // variables // p = 1
  int m; // states // m = 4
  int r; // disturbances // r = 4
  // system matrices
  cov_matrix[m] P1;
  vector[m] a1;
  // data
  matrix[1, n] y;
  matrix[m, m] T;
  matrix[p, m] Z;
  matrix[m, r] R; // includes the correlation between level & trend
  vector[p] c;
  vector[m] d;
}
parameters {
  real<lower=0.0> sigma2;
  vector<lower=0.0>[m - 1] tau;
  vector<lower=0.0>[n] lambda[2];
}
transformed parameters {
  matrix[r, r] Q[n];
  vector<lower=0.0>[p] H;
  H[1] <- sigma2;
  for (i in 1:n) {
    real tau3_2;
    vector[r] Q_diag;
    tau3_2 <- pow(tau[3], 2);
    Q_diag[1] <- pow(tau[1] * lambda[1, i], 2);
    Q_diag[2] <- pow(tau[2] * lambda[2, i], 2);
    Q_diag[3] <- tau3_2;
    Q_diag[4] <- tau3_2;
    Q[i] <- diag_matrix(Q_diag);
  }
}
model {
  matrix[m, m] RQR[n];
  for (i in 1:n) {
    RQR[i] <- R * Q[i] * R';
  }
  KALMAN_SEQ_Q(y, T, Z, H, RQR, a1, P1, c, d);
  JEFFREYS(sigma2);
  tau ~ cauchy(0, sqrt(sigma2));
  for (i in 1:2) {
    lambda[i] ~ cauchy(0, 1);
  }
}
